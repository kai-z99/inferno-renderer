float DistributionGGX(vec3 N, vec3 H, float roughness)
{
    const float PI = 3.14159265359;

    float a      = roughness*roughness;
    float a2     = a*a;
    float NdotH  = max(dot(N, H), 0.0);
    float NdotH2 = NdotH*NdotH;

    float num   = a2;
    float denom = (NdotH2 * (a2 - 1.0) + 1.0);
    denom = PI * denom * denom;

    return num / denom;
}

float GeometrySchlickGGX(float NdotV, float roughness)
{
    float r = (roughness + 1.0);
    float k = (r*r) / 8.0;

    float num   = NdotV;
    float denom = NdotV * (1.0 - k) + k;

    return num / denom;
}

float GeometrySmith(vec3 N, vec3 V, vec3 L, float roughness)
{
    float NdotV = max(dot(N, V), 0.0);
    float NdotL = max(dot(N, L), 0.0);
    float ggx2  = GeometrySchlickGGX(NdotV, roughness); //geometry obstruction
    float ggx1  = GeometrySchlickGGX(NdotL, roughness); //geometry shadowing

    return ggx1 * ggx2;
}

vec3 FresnelSchlick(float cosTheta, vec3 F0)
{
    return F0 + (1.0 - F0) * pow(clamp(1.0 - cosTheta, 0.0, 1.0), 5.0);
}


//Directions/normal are assumed to be normalized!
vec3 DirLightEval_CookTorrance(vec3 lightCol, float lightPower, vec3 lightDir, vec3 viewDir, vec3 normal, vec3 albedo, float metallic, float roughness)
{
    const float PI = 3.14159265359;

    //0.04 is acceptable for most materials
    vec3 F0 = vec3(0.04);
    F0 = mix(F0, albedo, metallic); //albedo stores scatter color of dialectrics, base reflectivity metals. (2 pipelines)

    vec3 halfway = normalize(-viewDir + -lightDir); //micro facet normal

    vec3 radiance = lightCol * lightPower;
    vec3 fLambert = albedo / PI;

    //cook-torrence BRDF
    float NDF = DistributionGGX(normal, halfway, roughness);
    float G = GeometrySmith(normal, -viewDir, -lightDir, roughness);
    vec3 F = FresnelSchlick(max(dot(halfway, -viewDir), 0.0f), F0);
    vec3 numerator = NDF * G * F;
    float denominator = 4.0 * max(dot(-viewDir, normal), 0.0) * max(dot(-lightDir, normal), 0.0) + 0.00001;
    vec3 fCookTorrence = numerator / denominator;

    vec3 kS = F;
    vec3 kD = vec3(1.0) - kS;
    kD *= 1.0 - metallic; //kd theoretically either exists or is 0. (binary metal values)

    float NdotL = max(dot(normal, -lightDir),0.0f);
    return (kD * fLambert + fCookTorrence) * radiance * NdotL;
}